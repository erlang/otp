#!/usr/bin/env escript
%% -*- erlang -*-

%% Parse address sanitizer log files generated from test runs with
%% with environment variables ASAN_LOG_DIR and TS_RUN_EMU=asan set.

%% Repeated leak reports are ignored and additional leaks of same type
%% as seen before are identified as such.

-mode(compile).

main([]) ->
    help();
main(["--help"]) ->
    help();
main([OutDir]) ->
    case os:getenv("ASAN_LOG_DIR") of
	false ->
	    io:format(standard_error,
		      "\nMissing asan log directory argument and environment\n"
		      "variable ASAN_LOG_DIR is not set.\n\n",[]),
	    help();
	InDir ->
	    run(OutDir, InDir)
    end;
main([OutDir, InDir]) ->
    run(OutDir, InDir).


help() ->
    io:format("\nSyntax: asan_log_to_html OutDir [InDir]\n"
	      "\nParses all address-sanetizer log files in InDir\n"
	      "and generates a summary file OutDir/asan_summary.html.\n"
	      "Environment variable ASAN_LOG_DIR is used if InDir\n"
	      "is not specified\n\n", []).

-record(logacc, {srcfile,            % full path of current log file
                 did_output = false, % output contribution from srcfile
                 app = none,         % current application
                 app_err = 0,        % nr of reported errors from application
                 tc_err = 0}).       % nr of reported errors from srcfile (test case)

run(OutDir, InDir) ->
    {ok, InFilesUS} = file:list_dir(InDir),
    InFiles = lists:sort(InFilesUS),

    OutFile = filename:join(OutDir, "asan_summary.html"),
    {ok, Out} = file:open(OutFile, [write]),

    ok = file:write(Out, <<"<!DOCTYPE html>\n"
                          "<html>\n"
                          "<head><title>Address Sanitizer</title>\n">>),
    ok = file:write(Out, style_block()),
    ok = file:write(Out, <<"</head><body>\n"
		          "<h1>Address Sanitizer</h1>\n">>),

    {_, _, LogAcc2} =
        lists:foldl(fun(File, {LM, RegEx, LogAcc}) ->
                            analyze_log_file(Out, filename:join(InDir,File),
                                             {LM, RegEx, LogAcc})
                    end,
                    {#{}, none, #logacc{}},
                    InFiles),

    LogAcc3 = app_end(Out, LogAcc2),
    try_delete_srcfile(LogAcc3),

    Time = calendar:system_time_to_rfc3339(erlang:system_time(second),
                                           [{time_designator, 32}]),
    %%{_, _, ThisFile} = code:get_object_code(?MODULE),
    ThisFile = escript:script_name(),
    User = string:trim(os:cmd("whoami")),
    {ok, Host} = inet:gethostname(),
    ok = io:format(Out, "\n<hr><p><small>This page was generated ~s\n"
                   " by <tt>~s</tt>\n"
                   " run by ~s@~s.</small></p>\n",
                   [Time, ThisFile, User, Host]),

    ok = file:write(Out, script_block()),
    ok = file:write(Out, <<"</body>\n</html>\n">>),
    ok = file:close(Out),
    io:format("Generated file ~s\n", [OutFile]),
    ok.

analyze_log_file(Out, SrcFile, {LeakMap0, RegEx0, LogAcc0}=Acc) ->

    #logacc{app=PrevApp} = LogAcc0,

    case filelib:is_regular(SrcFile) of
        false ->
            Acc;
        true ->
            FileName = filename:basename(SrcFile),
            %%io:format("analyze ~s\n", [FileName]),

            %% Is it a new application?
            LogAcc2 = case string:lexemes(FileName, "-") of
                          [_Exe, PrevApp | _] ->
                              try_delete_srcfile(LogAcc0),
                              LogAcc0#logacc{srcfile=SrcFile,
                                             tc_err=0,
                                             did_output=false};
                          [_Exe, NewApp | _] ->
                              LogAcc1 = app_end(Out, LogAcc0),
                              try_delete_srcfile(LogAcc1),
                              LogAcc1#logacc{srcfile=SrcFile,
                                             app=NewApp,
                                             app_err=0,
                                             tc_err=0,
                                             did_output=false}
                      end,

            case LogAcc2#logacc.app_err of
                truncated ->
                    {LeakMap0, RegEx0, LogAcc2};
                _ ->
                    {ok, Bin} = file:read_file(SrcFile),
                    match_loop(Out, Bin, RegEx0, LogAcc2, 0, [], LeakMap0)
            end
    end.

-define(APP_ERR_LIMIT, 200).

match_loop(Out, _, RegEx, #logacc{app_err=AppErr}=LogAcc0, _, _, LM)
  when AppErr >= ?APP_ERR_LIMIT ->

    LogAcc1 = log_error(Out, LogAcc0),
    ok = io:format(Out, "<h2>WARNING!!! Log truncated for application ~p,"
                   " more than ~p errors found.</h2>\n",
                   [LogAcc1#logacc.app, ?APP_ERR_LIMIT]),
    {LM, RegEx, LogAcc1#logacc{app_err=truncated}};

match_loop(Out, Bin, RegEx0, LogAcc0, PrevEnd, Unmatched0, LM0) ->
    {Match, RegEx1} =
	run_regex(Bin, RegEx0,
		  %% LeakReport
		  "(?:(Direct|Indirect) leak of ([0-9]+) byte\\(s\\) "
		  "in ([0-9]+) object\\(s\\) allocated from:\n"
		  "((?:[ \t]*#[0-9]+.+\n)+))" % Call stack
		  "|"
		  %% ErrorReport
		  "(?:(==ERROR: AddressSanitizer:.*\n"
		  "(?:.*\n)+?)"   % any lines (non-greedy)
		  "^(?:==|--))"   % stop at line begining with == or --
		  "|"
		  %% Skipped
		  "(?:^[=-]+$)"  % skip lines consisting only of = or -
                  "|"
                  "Objects leaked above:\n" % if LSAN_OPTIONS="report_objects=1"
                  "(?:0x.+\n)+"
                  "|"
                  "^\n", % empty lines
		  [multiline],
		  [{offset, PrevEnd}, {capture, all, index}]),


    BP = fun(PartIx) -> binary:part(Bin, PartIx) end,

    case Match of
        [ErrorReport, {-1,0}, {-1,0}, {-1,0}, {-1,0}, Captured] ->
            {Start,MatchLen} = ErrorReport,
            LogAcc1 = log_error(Out, LogAcc0),
            ok = io:format(Out, "<p><pre~s>\n", [style(error)]),
            ok = file:write(Out, BP(Captured)),
            ok = io:format(Out, "</pre></p>\n", []),
            Unmatched1 = [BP({PrevEnd, Start-PrevEnd}) | Unmatched0],
            End = Start + MatchLen,
            match_loop(Out, Bin, RegEx1, LogAcc1, End, Unmatched1, LM0);

        [LeakReport, TypeIx, BytesIx, BlocksIx, StackIx | _] ->
            {Start, MatchLen} = LeakReport,
            Bytes = binary_to_integer(BP(BytesIx)),
            Blocks = binary_to_integer(BP(BlocksIx)),
            End = Start + MatchLen,
            Unmatched1 = [BP({PrevEnd, Start-PrevEnd})|Unmatched0],
            TypeBin = BP(TypeIx),

            %% We indentify a leak by its type (direct or indirect)
            %% and its full call stack.
            Key = {TypeBin, BP(StackIx)},
            {LogAcc2, LM2} =
                case lookup_leak(LM0, Key) of
                    undefined ->
                        %% A new leak
                        LM1 = insert_leak(LM0, Key, Bytes, Blocks),
                        LogAcc1 = log_error(Out, LogAcc0),
                        ok = io:format(Out, "<p><pre~s>\n", [style(new, TypeBin)]),
                        ok = file:write(Out, BP(LeakReport)),
                        ok = io:format(Out, "</pre></p>\n", []),
                        {LogAcc1, LM1};

                    {Bytes, Blocks} ->
                        %% Exact same leak(s) repeated, ignore
                        {LogAcc0, LM0};

                    {OldBytes, OldBlocks} ->
                        %% More leaked bytes/blocks of same type&stack as before
                        LM1 = insert_leak(LM0, Key, Bytes, Blocks),
                        LogAcc1 = log_error(Out, LogAcc0),
                        ok = io:format(Out, "<p><pre~s>\n", [style(more, TypeBin)]),
                        ok = io:format(Out, "More ~s leak of ~w(~w) byte(s) "
                                       "in ~w(~w) object(s) allocated from:\n",
                                       [TypeBin, Bytes - OldBytes, Bytes,
                                        Blocks - OldBlocks, Blocks]),
                        ok = file:write(Out, BP(StackIx)),
                        ok = io:format(Out, "</pre></p>\n", []),
                        {LogAcc1, LM1}
                end,
            match_loop(Out, Bin, RegEx1, LogAcc2, End, Unmatched1, LM2);

        [SkipLine] ->
            {Start, MatchLen} = SkipLine,
            %%nomatch = binary:match(BP(SkipLine), <<"\n">>), % Assert single line
            End = Start + MatchLen,
            Unmatched1 = [BP({PrevEnd, Start-PrevEnd})|Unmatched0],
            match_loop(Out, Bin, RegEx1, LogAcc0, End, Unmatched1, LM0);

        nomatch ->
            Unmatched1 = [BP({PrevEnd, byte_size(Bin)-PrevEnd}) | Unmatched0],

            LogAcc2 =
                case iolist_size(Unmatched1) > 500 of
                    true ->
                        LogAcc1 = log_error(Out, LogAcc0),
                        ok = io:format(Out, "<h2>WARNING!!! May be unmatched error reports"
                                       " in file ~s:</h2>\n<p><pre>~s</pre></p>",
                                       [LogAcc1#logacc.srcfile, Unmatched1]),
                        LogAcc1#logacc{tc_err = LogAcc1#logacc.tc_err+1};
                    false ->
                        LogAcc0
                end,
            {LM0, RegEx1, LogAcc2}
    end.

lookup_leak(LeakMap, Key) ->
    maps:get(Key, LeakMap, undefined).

insert_leak(LeakMap, Key, Bytes, Blocks) ->
    LeakMap#{Key => {Bytes, Blocks}}.

log_error(Out, #logacc{app_err=AppErr, tc_err=TcErr}=LogAcc) ->
    DidApp = case AppErr of
                 0 ->
                     %% First error in application, print clickable app button
                     ok = io:format(Out, "<button type=\"button\" "
                                    "class=\"app_err\">"
                                    "~s</button>\n"
                                    "<div class=\"content\">", [LogAcc#logacc.app]),
                     true;
                 _ ->
                     false
             end,

    DidTc = case TcErr of
                0 ->
                    %% First error in test case, print test case header
                    SrcFile = LogAcc#logacc.srcfile,
                    TcFile = filename:basename(SrcFile),
                    case string:lexemes(TcFile, "-") of
                        [_Exe, App, _Rest] ->
                            ok = io:format(Out, "<h3>Before first test case of ~s</h3>\n",
                                           [App]);
                        [_Exe, _App, "tc", Num, Mod, Rest] ->
                            [Func | _] = string:lexemes(Rest, "."),
                            ok = io:format(Out, "<h3>Test case #~s ~s:~s</h3>\n", [Num, Mod, Func]);
                        _ ->
                            ok = io:format(Out, "<h3>Strange log file name '~s'</h3>\n",
                                           [SrcFile])
                    end,
                    true;
                _ ->
                    false
            end,
    LogAcc#logacc{app_err=AppErr+1, tc_err=TcErr+1,
                  did_output = (LogAcc#logacc.did_output or DidApp or DidTc)}.

app_end(Out, LogAcc) ->
    case LogAcc of
        #logacc{app=none} ->
            LogAcc;
        #logacc{app_err=0} ->
            ok = io:format(Out, "<button class=\"app_ok\" disabled>~s</button>\n",
                           [LogAcc#logacc.app]),
            LogAcc#logacc{did_output = true};
        #logacc{} ->
            ok = io:format(Out, "</div>", []),
            LogAcc
    end.

style(error) ->
    " style=\"background-color:Tomato;\"".

style(new, <<"Direct">>) ->
    " style=\"background-color:orange;\"";
style(new, <<"Indirect">>) ->
    "";
style(more, _) ->
    " style=\"background-color:yellow;\"".


run_regex(Bin, none, RegExString, CompileOpts, RunOpts) ->
    {ok, RegEx} = re:compile(RegExString, CompileOpts),
    run_regex(Bin, RegEx, none, none, RunOpts);
run_regex(Bin, RegEx, _, _, RunOpts) ->
    case re:run(Bin, RegEx, RunOpts) of
	nomatch ->
	    {nomatch, RegEx};
	{match, Match} ->
	    {Match, RegEx}
    end.

try_delete_srcfile(LogAcc) ->
    case LogAcc of
        #logacc{srcfile=undefined} ->
            ignore;
        #logacc{did_output=false} ->
            %% This file did not contribute any output.
            %% Optimize future script invokations by removing it.
            delete_file(LogAcc#logacc.srcfile);
        _ ->
            keep
    end.

delete_file(File) ->
    case filelib:is_regular(File) of
        true ->
            io:format("Delete file ~p\n", [File]),
            Dir = filename:dirname(File),
            Name = filename:basename(File),
            Trash = filename:join([Dir, "DELETED", Name]),
            ok = filelib:ensure_dir(Trash),
            ok = file:rename(File, Trash);
        false ->
            ignore
    end.

style_block() ->
    <<"<style>

.app_err, .app_ok {
  color: white;
  padding: 10px;
  width: 100%;
  /*border: none;*/
  text-align: left;
  /*outline: none;*/
  font-size: 15px;
}

.app_err {
  background-color: #D11;
  cursor: pointer;
}
.app_ok {
  background-color: #292;
}

.active, .app_err:hover {
  background-color: #F11;
}

.content {
  padding: 0 18px;
  display: none;
  overflow: hidden;
  background-color: #f1f1f1;
}
</style>
">>.

script_block() ->
    <<"<script>
var coll = document.getElementsByClassName(\"app_err\");
var i;

for (i = 0; i < coll.length; i++) {
  coll[i].addEventListener(\"click\", function() {
    this.classList.toggle(\"active\");
    var content = this.nextElementSibling;
    if (content.style.display === \"block\") {
      content.style.display = \"none\";
    } else {
      content.style.display = \"block\";
    }
  });
}
</script>
">>.
