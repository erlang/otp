#!/usr/bin/env escript
%% -*- erlang -*-

%% Parse address sanitizer log files generated from test runs with
%% with environment variables ASAN_LOG_DIR and TS_RUN_EMU=asan set.

%% Repeated leak reports are ignored and additional leaks of same type
%% as seen before are identified as such.

-mode(compile).

main([]) ->
    help();
main(["--help"]) ->
    help();
main([OutDir]) ->
    case os:getenv("ASAN_LOG_DIR") of
	false ->
	    io:format(standard_error,
		      "\nMissing asan log directory argument and environment\n"
		      "variable ASAN_LOG_DIR is not set.\n\n",[]),
	    help();
	InDir ->
	    run(OutDir, InDir)
    end;
main([OutDir, InDir]) ->
    run(OutDir, InDir).


help() ->
    io:format("\nSyntax: asan_log_to_html OutDir [InDir]\n"
	      "\nParses all address-sanetizer log files in InDir\n"
	      "and generates a summary file OutDir/asan_summary.html.\n"
	      "Environment variable ASAN_LOG_DIR is used if InDir\n"
	      "is not specified\n\n", []).

run(OutDir, InDir) ->
    {ok, InFilesUS} = file:list_dir(InDir),
    InFiles = lists:sort(InFilesUS),

    OutFile = filename:join(OutDir, "asan_summary.html"),
    {ok, FD} = file:open(OutFile, [write]),

    ok = file:write(FD, <<"<!DOCTYPE html>\n"
                          "<html>\n"
                          "<head><title>Address Sanitizer</title>\n">>),
    ok = file:write(FD, style_block()),
    ok = file:write(FD, <<"</head><body>\n"
		          "<h1>Address Sanitizer</h1>\n">>),

    {_, LastApp, LastErrCnt, _} =
        lists:foldl(fun(File, Acc) ->
                            io:format("analyze ~s\n", [File]),
                            analyze_log_file(filename:join(InDir,File),
                                             FD, Acc)
                    end,
                    {#{}, none, 0, none},
                    InFiles),

    app_end(FD, LastApp, LastErrCnt),

    Time = calendar:system_time_to_rfc3339(erlang:system_time(second),
                                           [{time_designator, 32}]),
    %%{_, _, ThisFile} = code:get_object_code(?MODULE),
    ThisFile = escript:script_name(),
    User = string:trim(os:cmd("whoami")),
    {ok, Host} = inet:gethostname(),
    ok = io:format(FD, "\n<hr><p><small>This page was generated ~s\n"
                   " by <tt>~s</tt>\n"
                   " run by ~s@~s.</small></p>\n",
                   [Time, ThisFile, User, Host]),

    ok = file:write(FD, script_block()),
    ok = file:write(FD, <<"</body>\n</html>\n">>),
    ok = file:close(FD),
    io:format("Generated file ~s\n", [OutFile]),
    ok.

analyze_log_file(SrcFile, OutFD, {LeakMap0, PrevApp, ErrCnt0, RegEx0}) ->

    %% Is it a new application?
    {App,ErrCnt1} = case string:lexemes(filename:basename(SrcFile), "-") of
                        [_Exe, PrevApp | _] ->
                            {PrevApp, ErrCnt0};
                        [_Exe, NewApp | _] ->
                            app_end(OutFD, PrevApp, ErrCnt0),
                            {NewApp, 0}
                    end,

    {ok, Bin} = file:read_file(SrcFile),

    {Leaks, RegEx1} =
	run_regex(Bin, RegEx0,
		  %% LeakReport
		  "(?:(Direct|Indirect) leak of ([0-9]+) byte\\(s\\) "
		  "in ([0-9]+) object\\(s\\) allocated from:\n"
		  "((?:[ \t]*#[0-9]+.+\n)+))" % Call stack
		  "|"
		  %% ErrorReport
		  "(?:(==ERROR: AddressSanitizer:.*\n"
		  "(?:.*\n)+?)"   % any lines (non-greedy)
		  "^(?:==|--))"   % stop at line begining with == or --
		  "|"
		  %% Skipped
		  "(?:^[=-]+$)"  % skip lines consisting only of = or -
                  "|"
                  "Objects leaked above:\n" % if LSAN_OPTIONS="report_objects=1"
                  "(?:0x.+\n)+"
                  "|"
                  "^\n", % empty lines
		  [multiline],
		  [global, {capture, all, index}]),

    %% We indentify a leak by its type (direct or indirect)
    %% and its full call stack.

    BP = fun(PartIx) -> binary:part(Bin, PartIx) end,

    LeakChecker =
        fun([ErrorReport, {-1,0}, {-1,0}, {-1,0}, {-1,0}, Captured],
            {Out, PrevEnd, Unmatched0, LM0}) ->
                {Start,MatchLen} = ErrorReport,
                FD = fd(Out),
                ok = io_format(FD, "<p><pre~s>\n", [style(error)]),
                ok = file_write(FD, BP(Captured)),
                ok = io_format(FD, "</pre></p>\n", []),
                Unmatched1 = [BP({PrevEnd, Start-PrevEnd}) | Unmatched0],
                End = Start + MatchLen,
                {FD, End, Unmatched1, LM0};

           ([LeakReport, TypeIx, BytesIx, BlocksIx, StackIx | _],
            {Out, PrevEnd, Unmatched0, LM0}) ->
                {Start, MatchLen} = LeakReport,
                Bytes = binary_to_integer(BP(BytesIx)),
                Blocks = binary_to_integer(BP(BlocksIx)),
                End = Start + MatchLen,
                Unmatched1 = [BP({PrevEnd, Start-PrevEnd})|Unmatched0],
                TypeBin = BP(TypeIx),
                Key = {TypeBin, BP(StackIx)},
                case lookup_leak(LM0, Key) of
                    undefined ->
                        %% A new leak
                        LM1 = insert_leak(LM0, Key, Bytes, Blocks),
                        FD = fd(Out),
                        ok = io_format(FD, "<p><pre~s>\n", [style(new, TypeBin)]),
                        ok = file_write(FD, BP(LeakReport)),
                        ok = io_format(FD, "</pre></p>\n", []),
                        {FD, End, Unmatched1, LM1};

                    {Bytes, Blocks} ->
                        %% Exact same leak(s) repeated, ignore
                        {Out, End, Unmatched1, LM0};

                    {OldBytes, OldBlocks} ->
                        %% More leaked bytes/blocks of same type&stack as before
                        LM1 = insert_leak(LM0, Key, Bytes, Blocks),
                        FD = fd(Out),
                        ok = io_format(FD, "<p><pre~s>\n", [style(more, TypeBin)]),
                        ok = io_format(FD, "More ~s leak of ~w(~w) byte(s) "
                                       "in ~w(~w) object(s) allocated from:\n",
                                       [TypeBin, Bytes - OldBytes, Bytes,
                                        Blocks - OldBlocks, Blocks]),
                        ok = file_write(FD, BP(StackIx)),
                        ok = io_format(FD, "</pre></p>\n", []),
                        {FD, End, Unmatched1, LM1}
                end;
	   ([SkipLine], {Out, PrevEnd, Unmatched0, LM0}) ->
                {Start, MatchLen} = SkipLine,
		%%nomatch = binary:match(BP(SkipLine), <<"\n">>), % Assert single line
                End = Start + MatchLen,
                Unmatched1 = [BP({PrevEnd, Start-PrevEnd})|Unmatched0],
		{Out, End, Unmatched1, LM0}
        end,
    Out0 = {OutFD, App, ErrCnt1, SrcFile},
    {Out1, LastEnd, Unmatched1, LeakMap1} = lists:foldl(LeakChecker,
                                                 {Out0, 0, [], LeakMap0},
                                                 Leaks),

    Unmatched2 = [BP({LastEnd, byte_size(Bin)-LastEnd}) | Unmatched1],

    Out2 = case iolist_size(Unmatched2) > 500 of
               true ->
                   FD = fd(Out1),
                   ok = io_format(FD, "<h2>WARNING!!! May be unmatched error reports"
                                  " in file ~s:</h2>\n<p><pre>~s</pre></p>",
                                  [SrcFile, Unmatched2]),
                   FD;
               false ->
                   Out1
           end,
    App = element(2, Out2),
    ErrCnt2 = element(3, Out2),
    {LeakMap1, App, ErrCnt2, RegEx1}.

lookup_leak(LeakMap, Key) ->
    maps:get(Key, LeakMap, undefined).

insert_leak(LeakMap, Key, Bytes, Blocks) ->
    LeakMap#{Key => {Bytes, Blocks}}.

io_format({FD, _App, _ErrCnt}, Format, List) ->
    io:format(FD, Format, List).

file_write({FD, _App, _ErrCnt}, Bin) ->
    file:write(FD, Bin).

fd({FD, App, ErrCnt, SrcFile}) ->

    case ErrCnt of
        0 ->
            %% First error in application, print clickable app header
            ok = io:format(FD, "<button type=\"button\" "
                           "class=\"app_err\">"
                           "~s</button>\n"
                           "<div class=\"content\">", [App]);
        _ ->
            ok
    end,

    %% Print test case header
    TcFile = filename:basename(SrcFile),
    case string:lexemes(TcFile, "-") of
	[_Exe, App, _Rest] ->
	    ok = io:format(FD, "<h3>Before first test case of ~s</h3>\n",
			   [App]);
	[_Exe, _App, "tc", Num, Mod, Rest] ->
	    [Func | _] = string:lexemes(Rest, "."),
	    ok = io:format(FD, "<h3>Test case #~s ~s:~s</h3>\n", [Num, Mod, Func]);
	_ ->
	    ok = io:format(FD, "<h3>Strange log file name '~s'</h3>\n",
			   [SrcFile])
    end,
    {FD, App, ErrCnt+1};
fd({FD, App, ErrCnt}) ->
    {FD, App, ErrCnt+1}.

app_end(FD, App, ErrCnt) ->
    case {App, ErrCnt} of
        {none,_} -> ignore;
        {_, 0} ->
            ok = io:format(FD, "<button class=\"app_ok\" disabled>~s</button>\n", [App]);
        {_, _} ->
            ok = io:format(FD, "</div>", [])
    end.

style(error) ->
    " style=\"background-color:Tomato;\"".

style(new, <<"Direct">>) ->
    " style=\"background-color:orange;\"";
style(new, <<"Indirect">>) ->
    "";
style(more, _) ->
    " style=\"background-color:yellow;\"".


run_regex(Bin, none, RegExString, CompileOpts, RunOpts) ->
    {ok, RegEx} = re:compile(RegExString, CompileOpts),
    run_regex(Bin, RegEx, none, none, RunOpts);
run_regex(Bin, RegEx, _, _, RunOpts) ->
    case re:run(Bin, RegEx, RunOpts) of
	nomatch ->
	    {[], RegEx};
	{match, List} ->
	    {List, RegEx}
    end.

style_block() ->
    <<"<style>

.app_err, .app_ok {
  color: white;
  padding: 10px;
  width: 100%;
  /*border: none;*/
  text-align: left;
  /*outline: none;*/
  font-size: 15px;
}

.app_err {
  background-color: #D11;
  cursor: pointer;
}
.app_ok {
  background-color: #292;
}

.active, .app_err:hover {
  background-color: #F11;
}

.content {
  padding: 0 18px;
  display: none;
  overflow: hidden;
  background-color: #f1f1f1;
}
</style>
">>.

script_block() ->
    <<"<script>
var coll = document.getElementsByClassName(\"app_err\");
var i;

for (i = 0; i < coll.length; i++) {
  coll[i].addEventListener(\"click\", function() {
    this.classList.toggle(\"active\");
    var content = this.nextElementSibling;
    if (content.style.display === \"block\") {
      content.style.display = \"none\";
    } else {
      content.style.display = \"block\";
    }
  });
}
</script>
">>.
